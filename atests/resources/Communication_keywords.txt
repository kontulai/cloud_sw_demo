*** Settings ***
Library           Rammbock
Library           OperatingSystem
Library           BonkServer    127.0.0.1    55555
Resource          message_codes.txt

*** Keywords ***
Client connects to bonk server
    Create Tcp Client
    Client connects to tcp server    127.0.0.1    55555

Disconnect from bonkserver and delete client
    Delete Client

Server Has ${amount} bonks
    Write db    ${amount}

Server should have ${amount} bonks
    Client receives data
    ${server amount}=    Read db
    Should be equal as strings    ${server amount}    ${amount}

Client sends read-request
    Client sends read-request with value 0

Client sends read-request with value ${value}
    Reset Message
    Add Integer As Octets    ${READ AMOUNT MESSAGE CODE}    1
    Add Integer As Octets    ${value}    1
    Client sends data

Client sends increase-request with ${amount} as amount of bonks
    Reset message
    Add Integer As Octets    ${INCREASE AMOUNT MESSAGE CODE}    1
    Add Integer As Octets    ${amount}    1
    Client sends data

Client sends decrease-request with ${amount} as amount of bonks
    Reset message
    Add Integer As Octets    ${DECREASE AMOUNT MESSAGE CODE}    1
    Add Integer As Octets    ${amount}    1
    Client Sends Data

Client receives response with ${amount} as amount of bonks
    Client receives response with status ${OK} and value ${amount}

Client receives response with status ${status} and value ${amount}
    Client receives data
    ${message code}=    Read Integer From Octets    1
    ${received amount}=    Read Integer From Octets    1
    Should be equal    ${message code}     ${status}        Wrong response type
    Should be equal    ${received amount}    ${amount}   Wrong response value

Start bonkserver
    Start

Stop bonkserver
    Stop

Increase amount of bonks and check result from server
    [Arguments]    ${initial amount}    ${increment}    ${status}    ${final amount}
    Given client connects to bonk server
    and server has ${initial amount} bonks
    When client sends increase-request with ${increment} as amount of bonks
    Then client receives response with status ${status} and value ${final amount}
    [Teardown]    Disconnect from bonkserver and delete client

Decrease amount of bonks and check result from server
    [Arguments]    ${initial amount}    ${decrement}    ${status}    ${final amount}
    Given client connects to bonk server
    and server has ${initial amount} bonks
    When client sends decrease-request with ${decrement} as amount of bonks
    Then client receives response with status ${status} and value ${final amount}
    [Teardown]    Disconnect from bonkserver and delete client

