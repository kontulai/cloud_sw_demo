*** Settings ***
Library           Rammbock
Library           OperatingSystem
Library           BonkServer    127.0.0.1    55555
Resource          message_codes.txt

*** Keywords ***
Create client and connect to bonk server
    Create Tcp Client
    Client connects to tcp server    127.0.0.1    55555

Disconnect from bonkserver and delete client
    Delete Client

Server Has ${amount} bonks
    Write db    ${amount}

Server should have ${amount} bonks
    Client receives data
    ${server amount}=    Read db
    Should be equal as strings    ${server amount}    ${amount}

Client sends read-request
    Reset Message
    Add Integer As Octets    ${READ AMOUNT MESSAGE CODE}    1
    Add Integer As Octets    0    1
    Client sends data

Client sends increase-request with ${amount} as amount of bonks
    Reset message
    Add Integer As Octets    ${INCREASE AMOUNT MESSAGE CODE}    1
    Add Integer As Octets    ${amount}    1
    Client sends data

Client sends decrease-request with ${amount} as amount of bonks
    Reset message
    Add Integer As Octets    ${DECREASE AMOUNT MESSAGE CODE}    1
    Add Integer As Octets    ${amount}    1
    Client Sends Data

Client receives response with ${amount} as amount of bonks
    Client receives data
    ${message code}=    Read Integer From Octets    1
    ${received amount}=    Read Integer From Octets    1
    Should be equal    ${amount}    ${received amount}

Start bonkserver
    Start

Stop bonkserver
    Stop

Increase amount of bonks and check result from server
    [Arguments]    ${initial amount}    ${increment}    ${status}    ${final amount}
    Create client and connect to bonk server
    Given server has ${initial amount} bonks
    When client sends increase-request with ${increment} as amount of bonks
    Then client receives response with ${final amount} as amount of bonks
    [Teardown]    Disconnect from bonkserver and delete client

Decrease amount of bonks and check result from server
    [Arguments]    ${initial amount}    ${decrement}    ${status}    ${final amount}
    Create client and connect to bonk server
    Given server has ${initial amount} bonks
    When client sends decrease-request with ${decrement} as amount of bonks
    Client receives response with ${final amount} as amount of bonks
    [Teardown]    Disconnect from bonkserver and delete client

